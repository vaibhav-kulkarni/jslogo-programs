TO AKASH_KANDIL2 
CS  MAKE :S 71 PERSPECTIVE SETSC 0
FOR[G 1 71 10][
CS HOME   LT 90 FD 300 RT 90 HT
CUBE_DIS_OCTA :S :G 
HOME RT 90 DOWN 75 FD 250 LT 90 rr 45 ;rr 20 down 10 lt 30
OCTA_DIS_CUBE :S :G POLYVIEW WAIT 60]

END ;=================================================

TO CUBE_DIS_OCTA :S :G ;=============================================================
		; Program to draws Akash Kandil generated by moving out all faces of Cube to outside
		; it generates a rectangle of sides S x GD at each edge (12 of them) and an Equilateral 
		; triangle of side GD at each Corner (8 of them). These 8 triangles have faces parallel
		; to faces of Octahedron. thus there are total 26 faces
	MAKE "GD :G*SQRT 2 MAKE "R :G+:S/2 Make "ALFA ARCCOS(SQRT 1/3) 	
		; S Side of figures, H height of upper & lower layers 		
	  FD :S/2 DOWN 90 FD :R LT 90 BK :S/2 RR 90  ;Turtle is assumed to be 
		; faceing up at center of cube. FD S/2 and DOWN 90 will take to the plane of top edges of the 
		; side squares. FD R will take turtle to center of top edge of a side square. LT 90 & BK S/2
		; will take it to the corner. RR 90 will align it in the plane of side face of cube. Now it 
		; is ready to draw four faces of the Cube and four newly formed rectangular faces. 	 
	REPEAT 4 [ SETPC REPCOUNT SIDE :S :S FD :S DOWN 45 SETPC 2*REPCOUNT SIDE :GD :S FD :GD DOWN 45 ]  
		;Calls side routine for the face of Cube. Moved to other corner. DOWN 45 to get in plane of
		; newly generated rectangular face. Calls to draw this face of S x GD. Moves through GD to
		; next corner and DOWN 45 to aign along face of Cube ready to call for Cube face again. 
		
      FD :S/2 LR 90 RT 90 BK :R UP 90 BK :S/2 UP 180 SETPC 1
		; Oncompleting all 8 faces turtle back to a corner of a Cube face. FD S/2 LR 90 RT 90 BK :R
		; Brings it back to center of the polyhedra
	REPEAT 2 [ Down 180  FD :G+:S/2 DOWN 90 FD :S/2 RT 90 BK :S/2 SIDE :S :S  RT 90 
		; UP 90 to face UP FD G DOWN 90 brings it up and aligns itself to top face. FD S/2 takes it 
		; to center of a side RT 90 BK S/2 takes it to a corner and is ready to draw the top face. 
		; SIDE S S draws the face. RT 90 is required as side faces need right turns 	
      REPEAT 4 [ SETPC 1+REPCOUNT FD :S RT 45 DOWN :ALFA LT 30 TSIDE :GD RT 30 UP :ALFA LT 135 
	    RR 45 SIDE :S :GD LR 45 ] 
		; FD S brings it to next corner. By RT 45 turtle is
		; above median of Triangular face. Down ALFA takes turtle in the plane of the face along 
		; the Mediun. LT 30 the turtle is along a side is ready to draw that face. TSIDE GD draws 
		; the face. UP ALFA brings the turtle back to top plane. LT 135 baligns it in line with 
		; a side of rectangular face.RR 45 gets turtle in the plane of the face, ready to draw 
		; the face SIDE S GD draws the rectangular face. LR 45 brings turtle back to top face.
	LT 90 FD :S/2 RT 90 FD :S/2 DOWN 90 FD :G + :S/2 UP 180 ]
END ;=================================================END OF OPEN_PRISM

TO F_PYRAMID :R :NH :DTHV :THETAT  ; =========================================================
		;Program by Dr BARVE to draw a right frustum of Pyramid of NH number of sides 
		 ; and R the radius of circumscribing sphere top and bottom face. If sides are many it appears to be 
		 ; a frustum of cone. If top face degenerates to a point it become Pyramid or cone. only sides are 
		 ; top & Bottom are not drawn	
	
      MAKE "RT :R*SIN :THETAT            ;Radius of circumsribibg circle for top face
	MAKE "HTHH 180/:NH                 ; Half of angle subteded by one side in horizontal plane
	MAKE "ST 2*:RT*SIN :HTHH           ;Side of top face in the form of a polygon
	MAKE "THETAB (:THETAT +:DTHV )      ;Vertical angle at bottom face
	MAKE "HT COS :THETAT                ;Distace of top face from center of circumscribing sphere 
	MAKE "HB COS :THETAB                 ;Distace of bottom face from center of circumscribing sphere 
	MAKE "H :R*( :HT-:HB)                 ;Height of the frustum of Pyramid i.e. Thickness of a slice
 	MAKE "RB :R*SIN :THETAB               ;Radius of circumsribibg circle for top face 	
	MAKE "SB 2*:RB*SIN :HTHH              ;Side of bottom face in the form of a polygon	
	MAKE "MB :RB*COS :HTHH                   ;Mediun at bottom face of the triangle formed by side with center
	MAKE "MT :RT*COS :HTHH                  ;median at top face of the triangle formed by side with center
	MAKE "PHI ARCTAN (:MB - :MT)/:H         ;Angle of inclination of side face with horizontal
	MAKE "SLANT SQRT ( :H*:H + (:RB-:RT)*(:RB-:RT))  ;length of slant edge
	MAKE "SI ARCSIN (:SB - :ST)/(2*:SLANT)      ;Angle of slant edge with normal to base in the plane of side face		
      
		;Turtle is in the horizontle plane along one side of top face polygon
	 
       ST REPEAT :NH [ FD :ST  RT 360/:NH  LR 90 RR :PHI SIDE_FACE :ST :SB :SLANT :PHI :SI ] 
		;one by one NH side faces are drawn. FD :ST turtle move through one side, it turns right through 360/:NH
		;LR & RR as seperated as brackets are not allowed inside repeat. Face drawing starts now 
       LT 90 DOWN (90-:PHI) LT :SI FD :SLANT RT (90+:SI) RR (90-:PHI) 
     		;These commamnds are to take turtle to bottom face and then align with one of the edge in the 
            ; plane of bottom face. Turtle in the plane of top face along a side, LT 90 makes turtle face outwards
		; to move in the plane of slant face we have to nose down by (90-PHI) that is achieved by DOWN (90-PHI)
		; Now turn along slant edge by LT SI, Now FD slant will bring tutle down to bottom face level RT (90+SI)
		; will align turtle alng bottom edge of side face in the vertical plane. Now RR (90-PHI} Right Roll brings 
		; the turle in the plane of bottom face along a side ready to launch next frustum of Pyramid   
		    
END	; End of program F_PYRAMID =============================================

TO H2O ;===========
	HOME CS HT SETSC [0 0 0] ST  SETPC 5 PERSPECTIVE 
		; HOME sets turtle back to square one ;CS clears screen, SETSC sets screen color to black 
		; ( all zeros i.e. no color )	
      Make "ROLL 0 MAKE "PITCH 0 MAKE "TURN 0
	RR :ROLL UP :PITCH RT :TURN MAKE "RO 125 MAKE "RBOND 25 MAKE "RH 75 MAKE "BL 100 
      SPHERE :RO 20 20  WAIT 5 ST
	PU  WAIT 5 HOME  WAIT 5 DOWN 90  WAIT 5 RT 127  WAIT 5 FD (:RO-10)  WAIT 1  
	SETPC 2 OPEN_PRISM :RBOND :BL 10    
	PU HOME DOWN 90 RT 127 FD (:RO+:BL+:RH-15) LT 127 UP 90 RR :ROLL UP :PITCH RT :TURN PD SETPC 1
	SPHERE :RH 20 20
	PU HOME DOWN 90 LT 127 FD (:RO-10) ;RT 127  
	SETPC 2 OPEN_PRISM :RBOND :BL 10

      PU HOME DOWN 90 LT 127 FD (:RO+:BL+:RH-15) RT 127 UP 90 RR :ROLL UP :PITCH RT :TURN PD SETPC 1
	SPHERE :RH 20 20
      HT POLYVIEW          

END ; =============

	TO KOLAM3X21
	CS   SETSCREENCOLOR [200 100 30] MAKE "A 100 MAKE "R :A/4 SETPENSIZE [3 3] 
	PU BK :A LT 90 FD :A  RT 90 FD :A BK :R SETPENCOLOR 15

	REPEAT 3 [ PD CIRCLE 3 WAIT 20 PU BK :A PD CIRCLE 3 WAIT 20 RT 90 PU FD :A LT 90 FD :A ] HT ; GRID OF POINTS 

	SETPENCOLOR 1 SETTURTLE 1 WAIT 5 RT 90  SETTURTLE 1 HT WAIT 5
	SETPENCOLOR 2 SETTURTLE 2 WAIT 5 LT 90   SETTURTLE 2 HT WAIT 5

	ZCURVE :R 27 1 -1     	; Central top curve Angle of 135/5 = 27  
	ZLINE :A 1.4 5			; Central diagonal line
	ZCURVE :R 33 1 -1 		; Left & right bottom curves Angle 165/5 = 33
	ZLINE :A 0.88 4			; Left & Right lines going up
	ZCURVE :R 51 -1 1		;  Angle 255/5 = 51 left & right top curve
	ZLINE :A 1.4 5			; Left & Right diagonals
	ZCURVE :R 9 -1 1		; Angle 45/5 = 9 Bottom curve at center

	WAIT 50 HT SETTURTLE 1 HT
	END

TO OCTA_DIS_CUBE :S :G ;=============================================================		
		; Program to draws Akash Kandil generated by moving out all faces of Octahedron to outside
		; it generates a square at each corner of side GD(6 of them) and an rectangles of sides GD
		;  & S at each edge (12 of them). The 6 squares are parallel to the faces of a cube. Total 
		; faces 8 + 6 + 12 = 26  
	MAKE "S 100 MAKE "GD :G*SQRT 2 MAKE "R :G+:S/2 Make "ALFA ARCCOS(SQRT 1/3) Make "color 0	
		; S Side of figures, H height of upper & lower layers 		
	  FD :GD/2 DOWN 90  FD :R LT 90 BK :S/2 RR 90  ;Turtle is assumed to be 
		; faceing up at center of cube. FD S/2 and DOWN 90 will take to the plane of top edges of the 
		; side squares. FD R will take turtle to center of top edge of a side square. LT 90 & BK S/2
		; will take it to the corner. RR 90 will align it in the plane of side face of cube. Now it 
		; is ready to draw four faces of the Cube and four newly formed rectangular faces. 	 
	REPEAT 4 [ SETPC REPCOUNT SIDE :S :GD FD :S DOWN 45 SETPC 2*REPCOUNT SIDE :GD :GD FD :GD DOWN 45 ]  
		;Calls side routine for the face of Cube. Moved to other corner. DOWN 45 to get in plane of
		; newly generated rectangular face. Calls to draw this face of S x GD. Moves through GD to
		; next corner and DOWN 45 to aign along face of Cube ready to call for Cube face again. 
		
      FD :S/2 LR 90 RT 90 BK :R UP 90 BK :GD/2 UP 180 SETPC 1
		; Oncompleting all 8 faces turtle back to a corner of a Cube face. FD S/2 LR 90 RT 90 BK :R
		; Brings it back to center of the polyhedra
	REPEAT 2 [ Down 180  FD :GD/2+:S/SQRT 2 DOWN 90 RT 45 FD :GD/2 RT 90 BK :GD/2 SIDE :GD :GD  RT 90 
		; UP 90 to face UP FD G DOWN 90 brings it up and aligns itself to top face. FD S/2 takes it 
		; to center of a side RT 90 BK S/2 takes it to a corner and is ready to draw the top face. 
		; SIDE S S draws the face. RT 90 is required as side faces need right turns 	
      REPEAT 4 [ SETPC 1+REPCOUNT FD :GD RT 45 DOWN :ALFA LT 30 TSIDE :S RT 30 UP :ALFA LT 135 
	    RR 45 SIDE :GD :S LR 45 ] 
		; FD S brings it to next corner. By RT 45 turtle is
		; above median of Triangular face. Down ALFA takes turtle in the plane of the face along 
		; the Mediun. LT 30 the turtle is along a side is ready to draw that face. TSIDE GD draws 
		; the face. UP ALFA brings the turtle back to top plane. LT 135 baligns it in line with 
		; a side of rectangular face.RR 45 gets turtle in the plane of the face, ready to draw 
		; the face SIDE S GD draws the rectangular face. LR 45 brings turtle back to top face.
	LT 90  FD :GD/2  RT 90  FD :GD/2  DOWN 90  FD :GD/2 + :S/SQRT 2  UP 180 RR 45]
END ;=================================================END OF OPEN_PRISM

TO OPEN_PRISM :R :H :N  ;=============================================================
		; Program to draw Right prism without top and bottom. If sides are many it 
		;  appears to be cydender

	;HOME CS PERSPECTIVE       ; Goes in Perspective Mode
	;SETPC 5 SETSC 0 

	;MAKE "H 100 MAKE "N 5 MAKE "R 75 
      MAKE "TH 360/:N MAKE "ALFA (180-:TH)/2 MAKE "S (2*:R*COS :ALFA)
	;PRINT :ALFA MAKE "BETA (180-:TH-2*:ALFA) PRINT :BETA
		; H Height of Prism, N number of sides, R Radius of circumscribibg circle of the 
		;   face of prism,TH Angle subtended by side at center, ALFA Angle btween Radius 
		;   and side, S Side of face of prism 
	DOWN 90 WAIT 1  CIRCLE :R WAIT 1 FD :R WAIT 1 RT (180-:ALFA) WAIT 1  LR 90 PD ;Turtle is assumed to be 
		; faceing the prism along the axis of it. UP 90 goes in the face of prism, FD R moves to 
		; perimeter, RT 180-ALFA point along a side of face polygon LR 90 Rolls in the plane of 
		; the side face and is ready to draw a face from a corner.  
	 
	REPEAT :N [ SIDE :H :S FD :S DOWN :TH WAIT 1 ] WAIT 5 ; Calls side routine, FD S after drawing 
		; one face moves ahead to draw other face, Down 90 Nose dow TH to get in to other face

	HT ;POLYVIEW

;end
END ;=================================================END OF OPEN_PRISM

TO SIDE :H :S ; ===========================================
	polystart  ; Instruction to LOGO that this is a surface or sufaces of polygon
	REPEAT 2 [ FD :S WAIT 1 RT 90 WAIT 1 FD :H RT 90 ]
	POLYEND 	;End of a surface or surfaces of polygon
END ;=======================================END OF SIDE

TO SIDE_FACE :ST :SB :SLANT :PHI :SI  ; ======================================= Side Face routine
		; ST & SB sides of top and bottom faces, SLANT slant edge, PHI ;Angle of inclination of side face 
		; with horizontal, SI ;Angle of slant edge with normal to base in the plane of side face
      POLYSTART FD :ST  LT (90-:SI) FD :SLANT LT (90+:SI)
             FD :SB LT (90+:SI) FD :SLANT LT (90-:SI) RR (90-:PHI)  POLYEND
		;POLYSTART Tells computer that this is one face of the 3D object to be drawn, FD :ST  moves along top
		; face, LT (90-:SI) turns left through (90-SI) FD :SLANT  Moves through slant height LT (90+:SI)
		; FD :SB move through side of the bottom face, LT (90+:SI) turn left through (90+SI) FD :SLANT Move
		; through slant height LT (90-:SI) Left turn through (90-SI) RR (90-:PHI) Right Roll through (90-PHI)
		; to go in the top plane POLYEND tells LOGO that face drawing is over

END ; ===================================

TO SPHERE :R :NV :NH  ; Main program ==================================================================
		;Sphere by plotting of series of frustums of cone starting from one pole to other
		MAKE "DTHV 180/:NV 
		;  R radius of sphere, NV number of lattitudes, NH nunber of longitudes, 
		;  DTHV difference in two lattitudes
       PU FD :R PD ST ;adjusts turtle up R for better posioning of sphere, 
	DOWN 90  ;Puts turtle in horizontal plane, SETS pen color to Red
   FOR [ THETAT 0 (180-:DTHV) :DTHV][ F_PYRAMID :R :NH :DTHV :THETAT ]  
		;loop goes from 0 to one lattide less. It is the number of frustums being plotted 
END ;========================================================== END OF SSPHERE

TO TSIDE :S   ; ===========================================
	polystart  ; Instruction to LOGO that this is a surface or sufaces of polygon
	REPEAT 3 [ FD :S RT 120 ]
	POLYEND 	;End of a surface or surfaces of polygon
END ;=======================================END OF SIDE

	TO ZCURVE :R :ALFA :DIRECTION1 :DIRECTION2

	REPEAT :ALFA [ SETPENCOLOR 1 SETTURTLE 1 ST WAIT 1 RT :DIRECTION1*5 FD :R*3.14/36 
			    SETPENCOLOR 2 SETTURTLE 2 ST WAIT 1 RT :DIRECTION2*5 FD :R*3.14/36 ] ; Central top curve Angle of 135/5 = 27
	END

	TO ZLINE :A :L :IN
	REPEAT :A*:L/:IN [ SETPENCOLOR 1 SETTURTLE 1 ST WAIT 1 FD :IN 
			         SETPENCOLOR 2 SETTURTLE 2 ST WAIT 1 FD :IN  ] ;Central diagonal line	
	END

Make "100 71
Make "a 100
Make "alfa 72
Make "beta 0
Make "bl 100
Make "color 0
Make "dthv 9
Make "g 1
Make "gd 100.40916292849
Make "h 0.923374455364676
Make "hb -1
Make "height 50
Make "ht -0.987688340595138
Make "hthh 9
Make "mb 9.07548633308544e-15
Make "mt 11.5881372890605
Make "n 7
Make "nh 15
Make "nv 15
Make "phi -85.4441416269168
Make "pitch 0
Make "r 150
Make "radius 50
Make "rb 9.18861341181465e-15
Make "rbond 25
Make "rh 75
Make "ro 125
Make "roll 0
Make "rt 11.7325848780173
Make "s 15.4508497187474
Make "savepos [250 100 0]
Make "sb 2.87483164707743e-15
Make "si -8.97202662400327
Make "slant 11.7688643591767
Make "st 3.67076127786348
Make "stepp 2
Make "th 36
Make "thetab 180
Make "thetat 0
Make "ts 72.1248916810278
Make "turn 0
Make "w 10.471975511966
